apply plugin: 'com.android.library'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
group="com.github.xiaosw"

android {
    compileSdkVersion  COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION as String

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.main {
        jniLibs.srcDirs = ['/src/main/libs']

        jni.srcDirs = ['src/main/jni']
    }

}

greendao {
    schemaVersion 1
    daoPackage 'com.xiaosw.library.db.dao'
    targetGenDir 'src/main/java'
}

project.afterEvaluate {
    printf("===============start ndk build======================\n")
//    compileDebugNdk.dependsOn 'NDKBuildDebug'
//    compileReleaseNdk.dependsOn 'NDKBuildRelease'
//    clean.dependsOn 'NDKBuildClean'
    compileDebugNdk.dependsOn "buildJniByDebugTask"
    compileReleaseNdk.dependsOn "buildJniByReleaseTask"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile "junit:junit:${JUNIT_VERSION}"
    compile rootProject.ext.support
    compile rootProject.ext.multidex
    compile rootProject.ext.greendao
    // This is only needed if you want to use encrypted databases
    compile "net.zetetic:android-database-sqlcipher:${ANDROID_DATABASE_SQLCIPHER_VERSION}"
    compile "com.github.bumptech.glide:glide:${GLIDE_VERSION}"
    compile files('src/main/libs/nineoldandroids-2.4.0.jar')
    compile "com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}"

    //可选，用于生成application类
    compile("com.tencent.tinker:tinker-android-anno:${TINKER_ANDROID_ANNO_VERSION}") { changing = true }
    //tinker的核心库
    compile("com.tencent.tinker:tinker-android-lib:${TINKER_ANDROID_LIB_VERSION}") { changing = true }

    compile "org.jetbrains.kotlin:kotlin-stdlib:${KOTLIN_VERSION}"
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// build jni
// debug
task buildJniByDebugTask(type:Exec) {
    def buildCmd = getNDKBuildCmd()
    commandLine buildCmd,
            'V=0',
            '-j10',
            'NDK_DEBUG=1',
            "NDK_OUT=$buildDir/ndk/",
            "NDK_APP_DST_DIR=$projectDir/src/main/libs/\$(TARGET_ARCH_ABI)"
}

// release
task buildJniByReleaseTask(type:Exec) {
    def buildCmd = getNDKBuildCmd()
    commandLine buildCmd,
            'V=0',
            '-j10',
            'NDK_DEBUG=0',
            "NDK_OUT=$buildDir/ndk/",
            "NDK_APP_DST_DIR=$projectDir/src/main/libs/\$(TARGET_ARCH_ABI)"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
